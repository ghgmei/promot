<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		actionBarVisible="false" overlayControls="false" tabBarVisible="false" title="Dashboard" creationComplete="formload()">
	
	<fx:Script>
		<![CDATA[
			
			import com.google.zxing.BarcodeFormat;
			import com.google.zxing.BinaryBitmap;
			import com.google.zxing.BufferedImageLuminanceSource;
			import com.google.zxing.DecodeHintType;
			import com.google.zxing.Result;
			import com.google.zxing.client.result.ParsedResult;
			import com.google.zxing.client.result.ResultParser;
			import com.google.zxing.common.BitMatrix;
			import com.google.zxing.common.ByteMatrix;
			import com.google.zxing.common.GlobalHistogramBinarizer;
			import com.google.zxing.common.flexdatatypes.HashTable;
			import com.google.zxing.qrcode.QRCodeReader;
			import com.google.zxing.qrcode.detector.Detector;
			
			import spark.events.ViewNavigatorEvent;
			
			protected var camera:Camera;
			private var videoDisplay:Video=new Video(360, 360);
			private var qrReader:QRCodeReader;
			private var bmd:BitmapData;
			private var cameraStarted:Boolean = false;
			
			protected function button1_clickHandler(event:MouseEvent):void
			{
				if (!cameraStarted) {
					if (Camera.isSupported)
					{
						camera=Camera.getCamera();
						camera.setMode(360, 360, 24);
						
						videoDisplay.x = 360;
						sv.addChild(videoDisplay);
						
						videoDisplay.attachCamera(camera);
						videoDisplay.rotation=90;
						qrReader=new QRCodeReader;
						btn.label = "Scan Now";
						lbl.text = "";
						cameraStarted = true;
						
					}
					else {
						lbl.text = "no camera found";
					}
				}
				else {
					decodeSnapshot();
				}
			}
			
			public function decodeSnapshot():void
			{
				lbl.text="checking...";
				bmd=new BitmapData(300, 300);
				bmd.draw(videoDisplay, null, null, null, null, true);
				videoDisplay.cacheAsBitmap=true;
				videoDisplay.cacheAsBitmapMatrix=new Matrix;
				decodeBitmapData(bmd, 300, 300);
				bmd.dispose();
				bmd=null;
				System.gc();
			}
			
			public function decodeBitmapData(bmpd:BitmapData, width:int, height:int):void
			{
				var lsource:BufferedImageLuminanceSource=new BufferedImageLuminanceSource(bmpd);
				var bitmap:BinaryBitmap=new BinaryBitmap(new GlobalHistogramBinarizer(lsource));
				
				var ht:HashTable=null;
				ht=this.getAllHints();
				
				var res:Result=null;
				try {
					res=qrReader.decode(bitmap, ht);
				}
				catch (event:Error) {
				}
				
				if (res == null) {
					videoDisplay.clear();
					lbl.text="nothing decoded";
					enterPatientManual.visible=true;
					patientManual.visible=true;
					warning.visible=true;
				}
				else {
					var parsedResult:ParsedResult=ResultParser.parseResult(res);
					lbl.text=parsedResult.getDisplayResult();
					sv.removeChild(videoDisplay);
					cameraStarted = false;
					btn.label = "Start Camera";
					userClass.getInstance().patient = parsedResult.getDisplayResult();
					navigator.pushView(NewLabView);
				}
			}
			
			public function getAllHints():HashTable
			{
				var ht:HashTable=new HashTable;
				ht.Add(DecodeHintType.POSSIBLE_FORMATS, BarcodeFormat.QR_CODE);
				return ht;
				
			}
			
			protected function enterPatientManual_clickHandler(event:MouseEvent):void
			{
				userClass.getInstance().patient = patientManual.text;
				navigator.pushView(NewLabView);
			}
			
			
			private function formload():void
			{
				enterPatientManual.visible=false;
				patientManual.visible=false;
				warning.visible=false;
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:Label top="107" color="#232323" fontSize="30" fontWeight="normal" horizontalCenter="0"
			 text="Scan QR on patient card to proceed"/>
	<s:Label id="warning" top="871" color="#BB0A0A" fontSize="30" fontWeight="normal"
			 horizontalCenter="-1"
			 text="Or enter the patient ID found on the patient card."/>
	<s:Button id="backButton" x="55" y="1281" width="795" height="150" label="&lt; Back"
			  click="navigator.popView()" color="#747373" fontSize="70"/>
	
	
		<s:SpriteVisualElement id="sv" y="180" width="600" height="346" horizontalCenter="0"/>
		<s:Label id="lbl" y="529" width="591" height="138" color="#F41E1E" fontSize="50"
				 fontWeight="bold" horizontalCenter="0" textAlign="center" verticalAlign="middle"/>
		<s:Button id="btn" x="52" y="693" width="795" height="150" label="Start Camera"
				  click="button1_clickHandler(event)" fontSize="50"/>
	<s:TextInput id="patientManual" x="52" y="913" width="503" height="150" fontSize="80"/>
	<s:Button id="enterPatientManual" x="563" y="913" width="284" height="150" label="Go"
			  click="enterPatientManual_clickHandler(event)" fontSize="60"/>
	<s:Label y="30" width="632" color="#174573" fontSize="65" fontWeight="bold" horizontalCenter="0"
			 text="Laboratory" textAlign="center"/>

</s:View>
